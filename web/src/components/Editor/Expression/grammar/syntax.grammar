@top Program { expression }

@skip { space }

// attr:tracetest.span.type = "${env:TRACETEST_SPAN_TYPE}"
// att:tracetest.span.duration + 2 = 5 + env:TRACETEST_SPAN_DURATION
// 5 + 5 = 10
// 10 != 20
// "Jorge" == "Ecuador" + ${attr:http.message}
// att:http.message = "error at line ${attr:tracetest.span.duration}"

expression {Expression (Comparator Expression)*}

Expression {ComposedValue (Operator ComposedValue)*}
ComposedValue {OutsideInput (Pipe SupportedParsers)* | Duration | Number | ComposedString}
OutsideInput {Source Identifier}
Interpolation {OpenInterpolation OutsideInput CloseInterpolation}
ComposedString {Quotes (Interpolation | String)+ Quotes}
SupportedParsers {JsonPath}
JsonPath {"json_path '$" Word "'"}

@tokens {
  Identifier { $[a-zA-Z_.\-0-9]+ }
  Word { $[a-zA-Z_.\-0-9\{\}\[\]]+ }
  Pipe {"|"}
  Number { @digit+ }
  Comparator {"=" | "contains" | "!=" | "<" | ">" | "<=" | ">=" | "not-contains" }
  Duration { Number ("." Number)?(" ")?("ns" | "us" | "ms"| "s"| "m" | "h") }
  Operator {"+" | "-" | "*" | "/" | "%" | "^" }
  String {(![${}"])+}
  Quotes {'"'}
  OpenInterpolation { "${" }
  CloseInterpolation { "}" }
  Colon { ":" }
  Source { ("env" | "attr") Colon }

  @precedence { OpenInterpolation, String }
  @precedence { String, space }
  @precedence { Duration, Number }

  space { @whitespace+ }
}

@detectDelim
